	name	fsincos
	konst
PI2	DATAF	1.570796326794		;PI/2
IPI	DATAF	0.318309886184		;1/PI
MAX	DATAF	9099.0			;MAX VALUE OF ARGUMENT
PII	DATAE	3.14159265359		;PI OR CLOSE TO IT
HALF	DATAE	0.5
MONE	DATAF	-1.0
EPS	DATAF	1.90734863E-6
POLY					;SINE CURVE POLYNOMIAL COEFFICIENTS
	DATAF	-0.1666665668E00
	DATAF	 0.8333025139E-2
	DATAF	-0.1980741872E-3
	DATAF	 0.2601903036E-5

	normal

	global	fcos
fcos	SBR	15,R10			;COSINE FLAG
	SBR	14,R10			;SIGN FLAG
	FABS	R5,R0
	FA	R5,PI2
	BR	SINCOS

        global	fsin
fsin	RBR	15,R10			;SINE FLAG
	DLR	R5,R0			;NEGATIVE?
	BGE	SIN1
	FABS	R5,R0			;MAKE POSITIVE
	RBR	14,R10			;SIGN FLAG
	BR	SINCOS
SIN1	SBR	14,R10			;SIGN FLAG
SINCOS	FM	R5,IPI			;Y*1/PI
	FA	R5,HALF
	FIX	R0,R5
	DSRA	R0,16			;MAKE DOUBLE
	EFLT	R7,R0			;XN
	TBR	15,R1			;EVEN?
	BEZ	SIN4
	XORM	R10,16#0002#		;INVERT SIGN FLAG
SIN4	TBR	15,R10
	BEZ	SIN5
	EFS	R7,HALF
SIN5
	FABS	R0,R3			;ABS(X)
	SR     	R2,R2			;MAKE EXTENDED FLOAT
	EFM	R7,PII			;XN*PI
	EFSR	R0,R7			;f
	FABS	R3,R0
SIN7
	FC	R3,EPS			;STEP 11
	BLE	SINEX			;ALMOST DONE SIN(X) = X 
	FMR	R3,R3			;g=f * f
	DSR	R6,R6
	LISP	R8,6
SINPL	FA	R6,POLY,R8		;COMPUTE SINE FROM POLYNOMIAL APPROX
	FMR	R6,R3			;*g
	SISP	R8,2
	BGE	SINPL
	FMR	R6,R0			;STEP 14
	FAR	R0,R6
SINEX
	TBR	14,R10			;TEST SIGN
	BNZ	SINEX1
	FM	R0,MONE
SINEX1	urs	r15


;	FUNCTION A_ESQRT(X:LONG_FLOAT) RETURN LONG_FLOAT ;

	global sqrt

	konst

ZERO	DATAE	0.0
C1	DATAE	0.59016
C2	DATAE	0.41731
C3	DATAE	0.70710678119		;SQRT(.5)

	normal
sqrt
	pshm	r3,r14
	dlr	r3,r0
	lr	r5,r2
	EFC	R0,ZERO
	BEZ	SQRTX			;BYE
        BGT     NOERR
 	bpt
NOERR	LR	R6,R4			;EXTRACT EXPONENT
	XBR	R6
	SRA	R6,8
	LIM	R13,16#FF00#		;SET MASKS NEEDED LATER
	LIM	R14,16#00FF#
	ANDR	R4,R13			;EXP = 0 
	LR	R1,R4			;f
	EFM	R0,C1
	EFA	R0,C2			;R0=Y0
	DLR	R7,R3
	LR	R9,R5
	EFDR	R7,R0			;F/Y0
	EFAR	R0,R7			;Z
	DLR	R10,R3			;f
	LR	R12,R5
	EFDR	R3,R0			;F/Z
	PSHM	R2,R2
	LR	R2,R1			;.25*Z
	ANDR	R1,R13			;EXP=0
	LIM	R2,-2,R2
	ANDR	R2,R14			;ISOLATE EXPONENT
	ORR	R1,R2
	POPM	R2,R2
	EFAR	R0,R3			;R0=Y2
	EFDR	R10,R0
	EFAR	R0,R10
   	LR	R3,R1			;.5*y2
	ANDR	R1,R13			;EXP=0
	LIM	R3,-1,R3
	ANDR	R3,R14			;ISOLATE EXPONENT
	ORR	R1,R3
	TBR	15,R6			;EXPONENT ODD?
	BEZ	SQRT2			;NO
	EFM	R0,C3
	AISP	R6,1
SQRT2	DISP	R6,2
	LR	R3,R1
	ANDR	R1,R13			;EXP=0
	XBR	R3
	SRA	R3,8			;CURRENT EXPONENT
	AR	R3,R6			;NEW EXPONENT
	ANDR	R3,R14
	ORR	R1,R3
SQRTX	popm	r3,r14
	urs	r15
	END


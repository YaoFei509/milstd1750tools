	name	fsqrt

;	FUNCTION FSQRT(X:FLOAT) RETURN FLOAT ;
;	input:  float in R0/R1
;	output: float in R0/R1

	konst
C1	DATA	#4B8A,#5C00		;0.59016
C2	DATA	#6AD4,#D4FF		;0.41731
C3	DATA	#5A82,#7900		;SQRT(.5)= .707

	normal

  	global	fsqrt			;FIND SQUARE ROOT OF A FLOAT
fsqrt
	dlr	r3,r0
	DLR	R0,R3			;=0?
	BEZ	SQRTX			;BYE
        BGT     NOERR
 	bpt				; DOES NOT RETURN
NOERR
	LR	R6,R4			;EXTRACT EXPONENT
	XBR	R6
	SRA	R6,8
	LIM	R9,#FF00		;SET MASKS NEEDED LATER
	LIM	R10,#00FF
	ANDR	R4,R9			;EXP = 0 
	DLR	R0,R3			;f
	FM	R0,C1
	FA	R0,C2			;R0=Y0
	DLR	R7,R3
	FDR	R7,R0			;F/Y0
	FAR	R0,R7			;Z
	FDR	R3,R0			;F/Z
	LR	R2,R1
	ANDR	R1,R9			;EXP=0
	LIM	R2,-2,R2
	ANDR	R2,R10			;ISOLATE EXPONENT
	ORR	R1,R2
	FAR	R0,R3
	TBR	15,R6			;EXPONENT ODD?
	BEZ	SQRT2			;NO
	FM	R0,C3
	AISP	R6,1
SQRT2	DISP	R6,2
	LR	R2,R1
	ANDR	R1,R9			;EXP=0
	XBR	R2
	SRA	R2,8			;CURRENT EXPONENT
	AR	R2,R6			;NEW EXPONENT
	ANDR	R2,R10
	ORR	R1,R2

SQRTX	urs	r15
	END
